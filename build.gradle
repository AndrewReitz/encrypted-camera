buildscript {
  repositories {
    mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:0.11.+'
    classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.10.+'
    classpath 'com.jakewharton.hugo:hugo-plugin:1.1.+'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
  }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0

apply plugin: 'android-sdk-manager'
apply plugin: 'android'
apply plugin: 'crashlytics'
apply plugin: 'hugo'

repositories {
  mavenCentral()
  maven { url "http://JRAF.org/static/maven/2" /* for android-switch-backport */ }
  maven { url 'http://download.crashlytics.com/maven' }
}

dependencies {
  compile 'com.android.support:support-v4:19.1.+'
  compile 'com.google.guava:guava:17.0'
  compile 'com.intellij:annotations:12.0'

  compile 'com.crashlytics.android:crashlytics:1.+'
  compile 'com.jakewharton.timber:timber:2.4.+'
  compile 'com.jakewharton:butterknife:5.1.+'

  compile 'com.squareup:otto:1.3.+'
  compile "org.jraf:android-switch-backport:1.3"
  compile 'org.mindrot:jbcrypt:0.3m'

  compile 'com.github.frankiesardo:icepick:2.3.+'
  provided 'com.github.frankiesardo:icepick-processor:2.3.+'

  compile 'com.netflix.rxjava:rxjava-core:0.19.+'
  compile 'com.netflix.rxjava:rxjava-android:0.19.+'

  compile 'com.squareup.dagger:dagger:1.2.0'
  provided 'com.squareup.dagger:dagger-compiler:1.2.0'

  androidTestCompile 'org.mockito:mockito-core:1.9.5'
  androidTestCompile 'org.easytesting:fest-assert-core:2.0M10'
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
  compileSdkVersion 19
  buildToolsVersion '19.1.0'

  defaultConfig {
    minSdkVersion 14
    targetSdkVersion 19

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
    buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  signingConfigs {
    release {
      storeFile file(storeFilePath)
      storePassword keystorePassword
      keyAlias "encrypted-camera"
      keyPassword aliasKeyPassword
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.dev'
      versionNameSuffix '-dev'
    }

    release {
      signingConfig signingConfigs.release
    }
  }

  lintOptions {
    abortOnError false
  }
}
